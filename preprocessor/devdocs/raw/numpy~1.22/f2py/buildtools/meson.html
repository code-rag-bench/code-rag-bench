<h1 id="f2py-meson">Using via meson</h1> <p>The key advantage gained by leveraging <code>meson</code> over the techniques described in <a class="reference internal" href="distutils#f2py-distutils"><span class="std std-ref">Using via numpy.distutils</span></a> is that this feeds into existing systems and larger projects with ease. <code>meson</code> has a rather pythonic syntax which makes it more comfortable and amenable to extension for <code>python</code> users.</p> <div class="admonition note"> <p class="admonition-title">Note</p> <p>Meson needs to be at-least <code>0.46.0</code> in order to resolve the <code>python</code> include directories.</p> </div> <section id="fibonacci-walkthrough-f77"> <h2>Fibonacci Walkthrough (F77)</h2> <p>We will need the generated <code>C</code> wrapper before we can use a general purpose build system like <code>meson</code>. We will acquire this by:</p> <pre data-language="bash">python -n numpy.f2py fib1.f -m fib2
</pre> <p>Now, consider the following <code>meson.build</code> file for the <code>fib</code> and <code>scalar</code> examples from <a class="reference internal" href="../f2py.getting-started#f2py-getting-started"><span class="std std-ref">Three ways to wrap - getting started</span></a> section:</p> <pre data-language="meson">project('f2py_examples', 'c',
  version : '0.1',
  default_options : ['warning_level=2'])

add_languages('fortran')

py_mod = import('python')
py3 = py_mod.find_installation('python3')
py3_dep = py3.dependency()
message(py3.path())
message(py3.get_install_dir())

incdir_numpy = run_command(py3,
  ['-c', 'import os; os.chdir(".."); import numpy; print(numpy.get_include())'],
  check : true
).stdout().strip()

incdir_f2py = run_command(py3,
    ['-c', 'import os; os.chdir(".."); import numpy.f2py; print(numpy.f2py.get_include())'],
    check : true
).stdout().strip()

fibby_source = custom_target('fibbymodule.c',
                            input : ['fib1.f'],
                            output : ['fibbymodule.c'],
                            command : [ py3, '-m', 'numpy.f2py', '@INPUT@',
                            '-m', 'fibby', '--lower' ]
                            )

inc_np = include_directories(incdir_numpy, incdir_f2py)

py3.extension_module('fibby',
           'fib1.f',
           fibby_source,
           incdir_f2py+'/fortranobject.c',
           include_directories: inc_np,
           dependencies : py3_dep,
           install : true)
</pre> <p>At this point the build will complete, but the import will fail:</p> <pre data-language="bash">meson setup builddir
meson compile -C builddir
cd builddir
python -c 'import fib2'
Traceback (most recent call last):
File "&lt;string&gt;", line 1, in &lt;module&gt;
ImportError: fib2.cpython-39-x86_64-linux-gnu.so: undefined symbol: FIB_
# Check this isn't a false positive
nm -A fib2.cpython-39-x86_64-linux-gnu.so | grep FIB_
fib2.cpython-39-x86_64-linux-gnu.so: U FIB_
</pre> <p>Recall that the original example, as reproduced below, was in SCREAMCASE:</p> <pre data-language="fortran">C FILE: FIB1.F
      SUBROUTINE FIB(A,N)
C
C     CALCULATE FIRST N FIBONACCI NUMBERS
C
      INTEGER N
      REAL*8 A(N)
      DO I=1,N
         IF (I.EQ.1) THEN
            A(I) = 0.0D0
         ELSEIF (I.EQ.2) THEN
            A(I) = 1.0D0
         ELSE 
            A(I) = A(I-1) + A(I-2)
         ENDIF
      ENDDO
      END
C END FILE FIB1.F
</pre> <p>With the standard approach, the subroutine exposed to <code>python</code> is <code>fib</code> and not <code>FIB</code>. This means we have a few options. One approach (where possible) is to lowercase the original Fortran file with say:</p> <pre data-language="bash">tr "[:upper:]" "[:lower:]" &lt; fib1.f &gt; fib1.f
python -n numpy.f2py fib1.f -m fib2
meson --wipe builddir
meson compile -C builddir
cd builddir
python -c 'import fib2'
</pre> <p>However this requires the ability to modify the source which is not always possible. The easiest way to solve this is to let <code>f2py</code> deal with it:</p> <pre data-language="bash">python -n numpy.f2py fib1.f -m fib2 --lower
meson --wipe builddir
meson compile -C builddir
cd builddir
python -c 'import fib2'
</pre> <section id="automating-wrapper-generation"> <h3>Automating wrapper generation</h3> <p>A major pain point in the workflow defined above, is the manual tracking of inputs. Although it would require more effort to figure out the actual outputs for reasons discussed in <a class="reference internal" href="index#f2py-bldsys"><span class="std std-ref">F2PY and Build Systems</span></a>.</p> <p>However, we can augment our workflow in a straightforward to take into account files for which the outputs are known when the build system is set up.</p> <pre data-language="meson">project('f2py_examples', 'c',
  version : '0.1',
  default_options : ['warning_level=2'])

add_languages('fortran')

py_mod = import('python')
py3 = py_mod.find_installation('python3')
py3_dep = py3.dependency()
message(py3.path())
message(py3.get_install_dir())

incdir_numpy = run_command(py3,
  ['-c', 'import os; os.chdir(".."); import numpy; print(numpy.get_include())'],
  check : true
).stdout().strip()

incdir_f2py = run_command(py3,
    ['-c', 'import os; os.chdir(".."); import numpy.f2py; print(numpy.f2py.get_include())'],
    check : true
).stdout().strip()

fibby_source = custom_target('fibbymodule.c',
                            input : ['fib1.f'],
                            output : ['fibbymodule.c'],
                            command : [ py3, '-m', 'numpy.f2py', '@INPUT@',
                            '-m', 'fibby', '--lower' ])

inc_np = include_directories(incdir_numpy, incdir_f2py)

py3.extension_module('fibby',
           'fib1.f',
           fibby_source,
           incdir_f2py+'/fortranobject.c',
           include_directories: inc_np,
           dependencies : py3_dep,
           install : true)
</pre> <p>This can be compiled and run as before.</p> <pre data-language="bash">rm -rf builddir
meson setup builddir
meson compile -C builddir
cd builddir
python -c "import numpy as np; import fibby; a = np.zeros(9); fibby.fib(a); print (a)"
# [ 0.  1.  1.  2.  3.  5.  8. 13. 21.]
</pre> </section> </section> <section id="salient-points"> <h2>Salient points</h2> <p>It is worth keeping in mind the following:</p> <ul class="simple"> <li>
<code>meson</code> will default to passing <code>-fimplicit-none</code> under <code>gfortran</code> by default, which differs from that of the standard <code>np.distutils</code> behaviour</li> <li>It is not possible to use SCREAMCASE in this context, so either the contents of the <code>.f</code> file or the generated wrapper <code>.c</code> needs to be lowered to regular letters; which can be facilitated by the <code>--lower</code> option of <code>F2PY</code>
</li> </ul> </section><div class="_attribution">
  <p class="_attribution-p">
    &copy; 2005&ndash;2021 NumPy Developers<br>Licensed under the 3-clause BSD License.<br>
    <a href="https://numpy.org/doc/1.22/f2py/buildtools/meson.html" class="_attribution-link">https://numpy.org/doc/1.22/f2py/buildtools/meson.html</a>
  </p>
</div>
